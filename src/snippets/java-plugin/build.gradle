plugins {
    id 'java'
}

repositories {
    maven {
        name = 'Hytale'
        url = 'https://maven.hytale.com/release'
    }
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(java_version)
    withSourcesJar()
    withJavadocJar()
}

// Quiet warnings about missing Javadocs.
javadoc {
    options.addStringOption('Xdoclint:-missing', '-quiet')
}

// Adds the Hytale server as a build dependency, allowing you to reference and
// compile against their code.
dependencies {
    implementation "com.hypixel.hytale:Server:${server_version}"
}

// Create the working directory to run the server if it does not already exist.
def serverRunDir = file("$projectDir/run")
if (!serverRunDir.exists()) {
    serverRunDir.mkdirs()
}

// Resolve the Hytale install directory. Set the HYTALE_HOME environment
// variable to override the default path.
ext {
    hytaleHome = System.getenv('HYTALE_HOME') ?: "${System.getProperty('user.home')}/AppData/Roaming/Hytale"
}

// Updates the manifest.json file with the latest properties defined in the
// gradle.properties file. Currently we update the version and if packs are
// included with the plugin.
tasks.register('updatePluginManifest') {
    def manifestFile = file('src/main/resources/manifest.json')
    doLast {
        if (!manifestFile.exists()) {
            throw new GradleException("Could not find manifest.json at ${manifestFile.path}!")
        }
        def manifestJson = new groovy.json.JsonSlurper().parseText(manifestFile.text)
        manifestJson.Version = version
        manifestJson.IncludesAssetPack = includes_pack.toBoolean()
        manifestFile.text = groovy.json.JsonOutput.prettyPrint(groovy.json.JsonOutput.toJson(manifestJson))
    }
}

// Makes sure the plugin manifest is up to date.
tasks.named('processResources') {
    dependsOn 'updatePluginManifest'
}

tasks.register('runServer', JavaExec) {
    dependsOn 'processResources', 'classes'
    mainClass = 'com.hypixel.hytale.Main'
    classpath = sourceSets.main.runtimeClasspath
    workingDir = serverRunDir
    args '--allow-op', "--assets=$hytaleHome/install/$patchline/package/game/latest/Assets.zip"
    if (includes_pack.toBoolean()) {
        def resourcesOutput = layout.buildDirectory.dir("resources/main").get().asFile
        args "--mods=${resourcesOutput.absolutePath}"
    }
    if (load_user_mods.toBoolean()) {
        args "--mods=$hytaleHome/UserData/Mods"
    }
}
